// SCAPS script

clear all 
load definitionfile Numos CIGS baseline.def
clear actions 
action iv.doiv
action iv.startv 0.02
action iv.stopv 0.60
action iv.increment 0.02

action dark // the NUMOS "measurement" was in dark

load measurement.iv Numos Ex 1 dark.iv
get measurement.iv zu // this sets also zname and uname
set scriptvariable.xname Nt (CIGS)(1/cm3)
set scriptvariable.yname Chi_sq

//show scriptvariables 

// three initial guesses for the parameter d(CdS)
math filllogarithmic x 1e15 1e17 3

set scriptvariable.maxiteration 3
loop start 
set layer1.defect1.ntotal xvector[loopcounter] // set the CIGSdoping density from the 3 first elements of xvector
// show scriptvariables 
calculate singleshot // calculate IV(dark)
get iv vw // place IV in vectors v and w, and sets vname and wname
math chi_square_log yzuvw // calculate the Chi-square deviation between the measured and calculated IV
set scriptvariable.yvector[loopcounter] yvalue // and place it in yvector at it's place
// show scriptvariables 
loop stop 

set scriptvariable.mode 6         // means find a minimum (here a CV curve fit), the variable (here Nt) is of "logarithmic nature"
loop maxiteration 15               // do not exagerate
loop maxerror     1e-6                // do not exagerate

loop start                             // start the iteration to find the root

// use the dll dellivered with SCAPS

rundll scapsuserfunction               

// it looks only to xvector and yvector [0], [1] and [2] to find a better guess to make y = maximal
// the function returns xvalue as a better guess for the variable; it is placed in yvalue
// set this to Nt
// then place this better guess on xvector[0] (and push the rest upward)               

set layer1.defect1.ntotal xvalue // use the proposed new value as Nt(CIGS)
set scriptvariable.xvector xvalue      

calculate singleshot 

// places the new Chi_sq in yvector[0] and pushes the rest up
get iv vw // place IV in vectors v and w
math chi_square_log yzuvw // calculate the Chi-square deviation between the measured and calculated QE
set scriptvariable.yvector yvalue

// you can comment the show command below out when it runs to your satisfaction       
//  show scriptvariables                   

loop stop

plot drawversusindex x
plot drawversusindex y
show scriptvariables 


