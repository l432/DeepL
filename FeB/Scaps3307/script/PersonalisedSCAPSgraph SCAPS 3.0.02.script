// Example script SCAPS 2.10.3
// KD ELIS-Ugent 3-2011 
// MB 20-5-2011 adapted to the more comfortable SCAPS 3.0.02 features
// The free carrier density profiles and the shallow acceptor profile are recorded for different temperatures
// The free hole density is compared with the free electron density and the shallow acceptor density
// The results are plotted
// This script uses the get command to retrieve n(x) and p(x) at the final point of the iv calculation (here 0 V)
// it uses also the get function to retrieve NA: this works only if there is no grading
// (otherwise, NA(x) should be retrieved via the recorder: see script PersonalisedSCAPSgraph.script )

set errorhandling.toscreen

// to start from a well defined condition
clear all 
action dark // you can also set it to light, and look to the difference
clear actions
action iv.doiv
action iv.startv 0
action iv.stopv 0
action iv.points 2

//load all files of interest
//definition file (this can be omitted when the script is run from the user interface and a definition file was already loaded)
load definitionfile  Numos CIGS baseline.def        

//construction x-vector (holds Temperatures)
math filllinear x 260 360 6

// get the acceptor density NA
// this works only for a non-graded structure
// if the structure is graded, you should retrieve NA(x) from the recorder (see other script examples)
get layer1.na uvalue
// show scriptvariables 

// take care of a nice output
set scriptvariable.wname x (µm)
set scriptvariable.vname p/n (#)
set scriptvariable.uname p/NA (cm-3)

//set loop properties
loop  maxiteration 5 //loop from 0 to 5: maximum indexed subscript will be 5

//beginning of the loop
loop start

action    workingpoint.Temperature xvector[loopcounter]  //set new temperature
calculate singleshot 

//access data
get energybands.n wy
get energybands.p wz
math constant uwu // makes a uvector with the same length as wvector, and fills it with uvalue (=NA)

//do some math with the n and p densities
math divide  vzy // v = z/y -> p/n
math divide  uzu // u = z/u -> p/NA

//plot results
plot  draw wv // p/n <-> mesh
plot draw yz // p <-> n
plot draw wu // p/NA <-> mesh

loop stop
//end of the loop

//end of script
