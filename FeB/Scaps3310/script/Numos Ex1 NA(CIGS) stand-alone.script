// SCAPS script

// SCAPS script
clear all 
load definitionfile Numos CIGS baseline.def
clear actions 
action cv.docv 
action cv.startv -1
action cv.stopv 0.40 // better result when limiting the curvefit to 0.4 V (?)
action cv.increment 0.02

action dark // the NUMOS "measurement" was in dark

load measurement.cv Numos Ex 1 dark.cv
get measurement.cv zu // this sets also zname and uname
set scriptvariable.xname NA (CIGS)(1/cm3)
set scriptvariable.yname Chi_sq

// three initial guesses for the parameter d(CdS): 1E14, 1E15 and 1E16 /cm3
math filllogarithmic x 1E14 1E16 3

set scriptvariable.maxiteration 3
loop start 
set layer1.NA xvector[loopcounter] // set the CIGSdoping density from the 3 first elements of xvector 
calculate singleshot // calculate CV(dark)
get cv vw // place CV in vectors v and w
math chi_square yzuvw // calculate the Chi-square deviation between the measured and calculated QE
set scriptvariable.yvector[loopcounter] yvalue // and place it in yvector at it's place
loop stop 

set scriptvariable.mode 6         // means find a minimum (here a CV curve fit), the variable (here NA) is of "logarithmic nature"
loop maxiteration 15               // do not exagerate
loop maxerror     1e-6                // do not exagerate

loop start                             // start the iteration to find the root

// use the dll dellivered with SCAPS

rundll scapsuserfunction               

// it looks only to xvector and yvector [0], [1] and [2] to find a better guess to make y = maximal
// the function returns xvalue as a better guess for the variable; it is placed in yvalue
// set this to NA
// then place this better guess on xvector[0] (and push the rest upward)               

set layer1.NA xvalue // use the proposed new value as NA(CIGS)
set scriptvariable.xvector xvalue      

calculate singleshot 

// places the new Chi_sq in yvector[0] and pushes the rest up
get cv vw // place QE in vectors v and w
math chi_square yzuvw // calculate the Chi-square deviation between the measured and calculated QE
set scriptvariable.yvector yvalue

// you can comment the show command below out when it runs to your satisfaction       
//  show scriptvariables                   

loop stop
plot drawversusindex x
plot drawversusindex y
show scriptvariables 


