// SCAPS script
// Set up by MB 24-5-2011 to illustrate the script facilities new in SCAPS 3.0.02
// Warning: this script takes about 4 min of calculation time, and performs about 96 calculations
// Warning: a divergence message could pop up, but do not pay attention to it: just click OK
// or direct to outputt to the log file (by removing the comment markers from the line below)
// set errorhandling.overwritefile 

clear all 
load definitionfile Numos CIGS baseline.def

action workingpoint.numberofpoints 10

action qe.startlambda 300
action qe.stoplambda 550
action qe.increment 10

action iv.startv 0.02
action iv.stopv 0.62
action iv.increment 0.05

action cv.startv -1
action cv.stopv 0.4
action cv.increment 0.05

load measurement.qe Numos Ex 1 dark.qe
load measurement.iv Numos Ex 1 dark.iv
load measurement.cv Numos Ex 1 dark.cv

// start the "Gummel-like loop"
loop maxiteration 5                 // do not exagerate
loop maxerror     1e-4             // do not exagerate;

loop start                             // start the iteration to find the root

set scriptvariable.xindex loopcounter // to transfer to the three next scripts
math increment x // and to start from 1, not from 0
set scriptvariable.wvalue 0 // the sum of the 3 Chi_square results
// show scriptvariables 

run script Numos Ex1 optimise d(CdS).script
set scriptvariable.zvalue xvalue // the actual (on it's way to optimum) value of d(CdS) is stored in zvalue
math scalaradd wwy
// show scriptvariables 

run script Numos Ex1 optimise Nt(CIGS).script
set scriptvariable.vvalue xvalue // the actual (on it's way to optimum) value of Nt(CIGS) is stored in vvalue
math scalaradd wwy 

run script Numos Ex1 optimise NA(CIGS).script
set scriptvariable.uvalue xvalue // the actual (on it's way to optimum) value of NA(CIGS) is stored in uvalue
math scalaradd wwy // now wvalue contains the sum of the 3 Chi_squares: this is used as the total error
// show scriptvariables 

set scriptvariable.looperror wvalue

loop stop

show scriptvariables
// legend: d is in zvalue, Nt in uvalue and NA in vvalue
// the number of iterations in the last main loop is zindex (the d-loop), uindex (the Nt-loop) and vindex (the NA-loop)
// the number of main iterations is in xindex
// the vectors show the properties during the last sub-loop, this is the NA-loop


